# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Christopher Reimer <github@creimer.net>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu1~saucy1
pkgver=3.9.16
pkgrel=100
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=arch_gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland' 'libxkbcommon' 'glib2-ubuntu')
makedepends=('gobject-introspection' 'mesa')
checkdepends=('hicolor-icon-theme' 'xorg-server-xvfb')
provides=("gtk3=${pkgver}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=('etc/gtk-3.0/settings.ini')
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        "https://launchpad.net/~gnome3-team/+archive/gnome3-staging//+files/gtk+3.0_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        'arch_settings.ini')
sha512sums=('a79f9de7990b15ebdcbd516d566ba32979bc8bad840463cbc9ee6ed3f99b011d1fc7de2e648037948b2dc999263d03bd92e0c8034bb3eefff7af241e3a1ba573'
            '7777f248e34c3bda358fed2bf3519c1281dea943578377c42b2ddddd9d0849719c8ac05a8a46ff0632edca05ffc63a151de798c6c09f3fdca493f8ae00ef2c39'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  autoreconf -vfi

  #CXX=/bin/false ./configure \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --enable-broadway-backend \
    --enable-wayland-backend \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

# Okay, these tests are obviously untested. There's no reason GTK 3 needs to be
# installed for them to run.
check() {
  cd "${srcdir}/gtk+-${pkgver}"

  #make -j1 check
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/arch_settings.ini" \
    "${pkgdir}/etc/gtk-3.0/settings.ini"
}

# vim:set ts=2 sw=2 et:
